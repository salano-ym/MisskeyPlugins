/// @ 0.15.0
### {
  name: "Word Replacer(View)"
  version: "0.1.3"
  author: "@salano_ym@misskey.io"
  description: "ノート閲覧時に特定の単語を置換して表示します"
  config: {
    repls: {
      type: 'string'
      label: '置換リスト'
      description: '例: ["old", "new"], ["旧", "新"] ...'
      default: ''
    }
  }
}

@validate(repls) {
  if Core:type(repls) != 'arr' {
    return [[] false]
  }
  let new_repls = []
  var ok = true
  each let r repls {
    if Core:type(r) != 'arr' || r.len != 2 || Core:type(r[0]) != 'str' || Core:type(r[1]) != 'str' {
      ok = false
    } else {
      new_repls.push(r)
    }
  }
  return [new_repls ok]
}


@replace(text repls) {
  if Core:type(text) != 'str' {
    return text
  }
  each let r repls {
    text = text.replace(r[0] r[1])
  }
  return text
}

let json_text = `[{Plugin:config.repls}]`
let a = if Json:parsable(json_text) {
  validate(Json:parse(json_text))
} else {
  [[] false]
}
let repls = a[0]
let is_valid_json = a[1]

@f(note) {
  note.text = replace(note.text repls)
  note.cw = replace(note.cw repls)
  if note.renote != null {
    note.renote.text = replace(note.renote.text repls)
    note.renote.cw = replace(note.renote.cw repls)
  }
  if note.reply != null {
    note.reply.text = replace(note.reply.text repls)
    note.reply.cw = replace(note.reply.cw repls)
  }
  return note
}

if is_valid_json {
  Plugin:register_note_view_interruptor(f)
} else {
  Mk:dialog('Word Replacer(View)' '置換リストが不正です' 'error')
}
